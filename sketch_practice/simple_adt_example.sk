adt node{
      Name{ int id; }
      Num{ int val; }
      Plus {node left; node right;}
      Mod {node left; node right;}
      Div {node left; node right;}
      NotEq {node left; node right;}
      Call {int id; node param;}
      If {node test; node then; node orelse;}
      Return {node retval;}
      FunctionDef {int id; Name arg; node stmt;}
 }

 //assume FunctionDef is parsed beforehand and the info is stored in node[m] functions
 int interpret([int n, int m], node e, int[n] assign, FunctionDef[m] functions){
      switch(e){
          case Name: return assign[e.id];
          case Num: return e.val;
          case Plus: return interpret(e.left,assign,functions) + interpret(e.right,assign,functions);
          case Mod: return interpret(e.left,assign,functions) % interpret(e.right,assign,functions);

          case Div: return interpret(e.left, assign,functions) / interpret(e.right, assign,functions);
          case NotEq: return interpret(e.left, assign,functions) != interpret(e.right, assign,functions);
          case If: {
                if(interpret(e.test,assign,functions) == 1){
                    return interpret(e.then,assign,functions);
                }
                else{
                    return interpret(e.orelse,assign,functions);
                }
          }
          case Return: return interpret(e.retval,assign,functions);
          case Call: {
               int paramval = interpret(e.param,assign,functions);
               assign[functions[e.id].arg.id] = paramval;
               return interpret(functions[e.id].stmt,assign,functions);
          }
          case FunctionDef: {
               functions[e.id] = e;
               return interpret(e.stmt,assign,functions);
          }
      }
      assert false;
 }



 void foo(int x);

 int sumDigits(int x){
 	if(x/10 != 0){
		return x%10 + sumDigits(x/10);
	}
	else{
		return x;
	}
 }

 harness void main (){
   node e = new FunctionDef(id=0,arg = new Name(id=0), stmt = new If(
              test=new NotEq(left=new Div(left=new Name(id = 0),right=new Num(val=10)),right=new Num(val=0)),
              then=new Return(retval=new Plus(left=new Mod(left=new Name(id=0),right=new Num(val=10)),right=new Call(id=0,param=new Div(left=new Name(id=0),right=new Num(val=10))))),
              orelse=new Return(retval=new Name(id=0))
            )
          );
   int V = 132;
   int[1] assign = {V};
   FunctionDef[1] functions;
   int ival = interpret(e,assign,functions);
   assert(ival == sumDigits(V));
   foo(ival);
 }
